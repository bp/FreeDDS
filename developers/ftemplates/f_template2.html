<!DOCTYPE html PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN">
<html>
  <head>
    <title>Data Dictionary System: Developers Fortran Templates (Template 2)</title>
    <meta http-equiv="Content-Type" content="text/html; charset=iso-8859-1">
    <meta name="keywords" 
      content="Data Dictionary System Free Seismic Processing System">
    <link href="../../styles.css" type="text/css" rel="stylesheet">
    <script src="../../javascript.js" language="JavaScript"
      type="text/javascript"></script>
    <script type="text/javascript">
      if (location.search.search("remove=") >= 0) {
         var tmp = "position:absolute;left:-9999px;";
      } else {
         var tmp = "";
      }
      document.write("<style type='text/css'>.comment "
         +"{color:#c00000;"+tmp+"}<\/style>");

      function setRemoveOption() {
         var tmp;
         if (location.search.search("remove=") >= 0) {
            tmp = "'Add Tutorial Comments'";
         } else {
            tmp = "'Remove Tutorial Comments'";
         }
         document.write("<center><form><input type='button' value="
            +tmp+" onclick='toggleComments();'><\/form><\/center>");
      }      
         
      function toggleComments() {
         if (location.search.search("remove=") >= 0) {
            document.location.search = "";
         } else {
            location = "?remove=";
         }
      }
    </script>
    <style type="text/css">
      pre {color:#000000;}
    </style>  
  </head>
  <body>
    <script type="text/javascript">
      header("developers","ftemplates","f_template2")
    </script>
    <!*********************************************************************>

    <script type="text/javascript">setRemoveOption();</script>

    <pre>
c***********************************************************************
c                     B P   A M E R I C A 
c             PROPRIETARY - TO BE MAINTAINED IN CONFIDENCE 
c                           COPYRIGHTED 2006   
c***********************************************************************
c
c                           TEMPLATE 2
c
c     <b>FORTRAN TEMPLATE DEMONSTRATING THE USE OF DDS CONVENIENCE </b>
c     <b>ROUTINES FOR A SIMPLE TRACE-TO-TRACE PROCESSING SCHEME </b>
c     <b>WITHOUT USING OR PASSING ANY TRACE HEADERS.</b>
c
c     Use a file extension of ".F" instead of ".f" to pull in the 
c     compiler preprocessor.
c
c     Written by Jerry Ehlers August 2006
c
cccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
c
      program template2
c
      implicit none
<span class="comment">c
c     INCLUDE THE DDS API (Application Program Interface)</span>
c
#include &lt;fdds.h&gt;
c
      integer in_bin,out_bin
      integer ier,ns
      real    scale
      character*80 title
c
      real    databuf(1)
      pointer (ptr_databuf,databuf)
c
      data    title/'template2: example of Fortran DDS program'/
      data    in_bin,out_bin/-1,-1/
c     
c=======================================================================
c     initialize
c=======================================================================
c
<span class="comment">c     OPEN THE PRINT FILE &amp; CHECK FOR HELP 
c     'fdds_openpr' function:
c          Open a printfile using the information automatically 
c          generated by RCS in '$Id: f_template2.html 125 2009-10-27 17:55:30Z ehlersjw $'.  If the user specifies some
c          form of help (-h help= HELP=...) on the command line,
c          then the return value will be < 0.
c</span>
      ier=<a href="../fapi/openpr.html">fdds_openpr</a>('f_template2', '$Id: f_template2.html 125 2009-10-27 17:55:30Z ehlersjw $')
      if (ier.gt.0) call help()
c
c=======================================================================
c     open input data file
c=======================================================================
c
<span class="comment">c     OPEN THE INPUT FILE 
c     'fddx_in' function:
c          This function opens the input file, with data Samples only 
c          as real values (unless input is complex).
c</span>
      in_bin=<a href="../fxapi/in.html">fddx_in</a>('in','stdin:',title)
<span class="comment">c
c     PRINT ERROR MESSAGE
c     'fdds_prterr' function:
c          Print an error message to the console (&amp; printfile if 
c          exists). Error count is kept by DDS.  Check for errors later, 
c          before processing to check for as many things as possible 
c          first.
c</span>
      if (in_bin.lt.0) then
         ier=<a href="../fapi/prterr.html">fdds_prterr</a>('Unable to open input data\n\0')
      endif
c
c     Get input data parameters
c
<span class="comment">c     RETRIEVE DATASET DEFINITIONS NEEDED BY APPLICATION 
c     'fdds_scanf' function:
c          Default values are assigned prior to definition retrieval.
c          A variable are unchanged, if it is not specified.
c
c          NOTE: What's special about the "size.axis(1)" definition?
c          'axis(N)' is automatically converted to the Nth axis name.
c          This allows hyper cube attributes to be retrieved by axis
c          number, instead of axis name.  For example, "size.axis(1)"
c          becomes "size.t" (assuming "axis=  t x y").
c
c          WARNING: Do NOT use %i, use %d to scan for integers because
c                   %i will interpret a value with a leading "0" as 
c                   octal instead decimal (eg. 010 would be read as
c                   8 instead of 10).
c</span>
      ier=<a href="../fapi/scanf.html">fdds_scanf</a>('size.axis(1)','%d\0',ns)
c
c=======================================================================
c     read parameters
c=======================================================================
c
<span class="comment">c     RETRIEVE COMMAND LINE PARAMETERS
c     'fdds_dict' function:
c          'fdds_dict' selects the 'par:' dictionary for scanning.
c          This dictionary only contains definitions from the command
c          line, and parameter dictionaries ("par=  fn1  fn2 ...").
c          Parameters ("in= ", "out_format= ", etc.) for the current
c          process can be read, without ambiguity from the input history 
c          (ie. local parameters only).
c</span>
      ier=<a href="../fapi/dict.html">fdds_dict</a>('par:','scan')
c
c     get parameters
c
      scale=1.0
      ier=<a href="../fapi/scanf.html">fdds_scanf</a>('scale','%f\0',scale)
c
c=======================================================================
c     print user parameters
c=======================================================================
c
<span class="comment">c     PRINT MESSAGES 
c     'fdds_prtmsg' function:
c          This function prints a formatted message to the print file
c          opened by "fdds_openpr"; otherwise to the console (stderr).
c
c     'fdds_prtcon' function:
c          This function prints a formatted message to the console and
c          to the print file if opened.
c
c     'fdds_prtmsg' function:
c          This function prints a formatted error message to the console 
c          and to the print file if opened.  It also keeps track of the
c          number of error messages printed for "fdds_closepr".
c</span>
      ier=<a href="../fapi/prtmsg.html">fdds_prtmsg</a>('\n*** USER PARAMETERS ***\n\n\0')
      ier=<a href="../fapi/prtmsg.html">fdds_prtmsg</a>('\tscale = %g\n\0',scale)
      ier=<a href="../fapi/prtmsg.html">fdds_prtmsg</a>('\n\0')
c
c=======================================================================
c     open the output file
c=======================================================================
c     
<span class="comment">c     CHECK FOR ERRORS BEFORE PROCEEDING ANY FURTHER 
c     'fdds_errors' function:
c          This function returns the number of errors reported using
c          "fdds_prterr".  No reason to create a new output if there
c          have been any errors.
c</span>
      if (fdds_errors().ne.0) goto 900
<span class="comment">c
c     CREATE OUTPUT FROM INPUT DICTIONARY 
c     'fddx_out' function:
c          This function will create an output dataset from the input
c          binary, passing all it's history information along.  
c          The user can override the output format and binary data by 
c          defining "out_format=" or "out_data=" since the first
c          argument is 'out'.
c
c          With the output convenience routines, the binaries are not 
c          actually opened until the binary tag is first used for I/O. 
c          This way the internal buffer definitions for the output file  
c          can be redefined (eg. axis, size, delta, origin...).  So no
c          need to check out_bin until after it really gets opened.
c</span>
      out_bin=<a href="../fxapi/out.html">fddx_out</a>('out','stdout:',title,in_bin)
<span class="comment">c
c     FORCE OPEN THE OUTPUT BINARIES
c     'fdds_lseek' function:
c          This function seeks to a specific trace position.  It is used 
c          here simply to force open the internal and output binaries.
c          Now we can check if it was really opened.
c</span>
      ier=<a href="../fapi/lseek.html">fdds_lseek</a>(out_bin,0,0,SEEK_SET)
      if (ier.lt.0) then
         ier=<a href="../fapi/prterr.html">fdds_prterr</a>('Unable to open output\n\0')
         goto 900
      endif
c
c=======================================================================
c     allocate dynamic arrays
c=======================================================================
c
      if (fdds_errors().ne.0) goto 900
c
c     Allocate memory for a single trace
c
<span class="comment">c     ALLOCATE DYNAMIC ARRAYS 
c     'fdds_malloc' function:
c          This function allocates memory or reports an error &amp; aborts.
c          Storage can be released by calling "fdds_free".
c</span>
      ptr_databuf=<a href="../fapi/malloc.html">fdds_malloc</a>(ns * SIZEOF_REAL)
c
c=======================================================================
c     process the data
c=======================================================================
c
      if (fdds_errors().ne.0) goto 900
c
      call doit(in_bin,out_bin,ns,scale,databuf)
c
c=======================================================================
c     close files, clean-up, &amp; exit
c=======================================================================
c
<span class="comment">c
c     CLOSE OUT
c     'fdds_close' function:
c          This function closes a dataset if the binary tag is &gt;= 0
c          including all underlying dictionaries and data structures.
c          This will also flush out all DDS data buffers out to the
c          kernel.
c
c     'fdds_closepr' function:
c          This function will close out the print file (if opened)
c          adding diagnostic information, unread command line 
c          parameters and termination status.  It will also exit
c          the program giving a usable status code.
c</span>
 900  ier=<a href="../fapi/close.html">fdds_close</a>(in_bin)
      ier=<a href="../fapi/close.html">fdds_close</a>(out_bin)
c
      ier=<a href="../fapi/closepr.html">fdds_closepr</a>()

      end
c
c***********************************************************************
c
c     doit
c
c***********************************************************************
c
      subroutine doit(in_bin,out_bin,ns,scale,databuf)
c
      implicit none
c
#include &lt;fdds.h&gt;
c
      integer in_bin,out_bin
      integer ns
      real    scale
      real    databuf(ns)
c
      integer ier,is
c
c     loop over each trace
c
<span class="comment">c
c     READ DATA
c     'fddx_read' function:
c          This function reads a specified number of traces.  
c          Check the number read in case of any problems.
c</span>
      ier=<a href="../fxapi/read.html">fddx_read</a>(in_bin,databuf,1)
      
      do while(ier.eq.1)
c
c        process the trace
c
         do is=1,ns
            databuf(is)=scale*databuf(is)
         enddo
<span class="comment">c
c        WRITE DATA
c        'fddx_write' function:
c             This function writes a specified number of traces.  
c             Check the number written in case of any problems.
c</span>
         ier=<a href="../fxapi/write.html">fddx_write</a>(out_bin,databuf,1)
         if (ier.ne.1) then
            ier=<a href="../fapi/prterr">fdds_prterr</a>('writing output\n\0')
            return
         endif
c
c        read the next trace
c
         ier=<a href="../fxapi/read.html">fddx_read</a>(in_bin,databuf,1)
      enddo
c
      return
      end
    </pre>

    <!*********************************************************************>
    <script type="text/javascript">footer()</script>
  </body>
</html>
