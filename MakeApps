#!/bin/csh -f
#
# Script to make and install all application programs and scripts
#

if ( "${DDSROOT}." == "." ) then
   echo "DDSROOT not defined"
   exit 
endif

if ( "${DDS_DIST}." == "." ) then
   echo "DDS_DIST not defined"
   exit 
endif

if ( ! $?HOST ) then
   set HOST = `uname -n`
endif
set log = ${DDSROOT}/mklog.${HOST}
rm -f ${log} >& /dev/null
touch ${log} >& /dev/null

#
# Build application libraries first
#
cd ${DDSROOT}/src/cmd
set progs = `ls lib*/Makefile`
foreach prog ( $progs )
   grep -l PrjLibs $prog >& /dev/null
   if ($status != 0) then
     set prog = ${prog:h}
     echo Building and Installing program: $prog
     echo >>& ${log}
     echo Building and Installing program: $prog >>& ${log}
     ( cd $prog;make;make install ) >>& ${log}
   endif
end
#
# Now build libraries dependent on others
#
set progs = `grep -l PrjLibs lib*/Makefile`
foreach prog ( $progs )
   set prog = ${prog:h}
   echo Building and Installing program: $prog
   echo >>& ${log}
   echo Building and Installing program: $prog >>& ${log}
   ( cd $prog;make;make install ) >>& ${log}
end

#
# Find all apps to make
#
cd ${DDSROOT}/src/cmd
set progs = `ls */Makefile`
foreach prog ( $progs )
   set lib=`echo $prog|awk -Fb '{print $1}'`
   if ("$lib" == "li") continue
   set prog = ${prog:h}
   echo Building and Installing program: $prog
   echo >>& ${log}
   echo Building and Installing program: $prog >>& ${log}
   ( cd $prog;make;make install ) >>& ${log}
end
#
# Check for new HPC architecture. 
#
if ( -d ${DDS_DIST}/asi ) then
  ls -tl ${DDS_DIST}/asi/*/*/mbs/prod/*
else
  ls -tl ${DDS_DIST}/*/*/mbs/prod/*
endif

#
# install any scripts
#
echo Installing scripts
cd ${DDSROOT}/src/scripts >>& ${log}
make

echo log in ${log}
