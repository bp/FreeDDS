#!/bin/csh -f
#
# This is a script to build several application list webpages
#
# Enable command line overrides
#

if ( ${#argv} > 0 ) then
   set DDS_DIST = "$1"
else if ( ! ${?DDS_DIST} ) then
   set DDS_DIST = "/hpc/tstapps/asi"
endif

if ( ${#argv} > 1 ) then
   set DDSROOT = "$2"
else if ( ! ${?DDSROOT} ) then
   set DDSROOT = "/hpc/tstapps/asi/src/dds"
endif

if ( ${#argv} > 2 ) then
   set DDS_SRC_REPOSITORY = "$3"
else if ( ! ${?DDS_SRC_REPOSITORY} ) then
   if ( -d $DDSROOT/src/cmd ) then
     set DDS_SRC_REPOSITORY = "$DDSROOT/src/cmd"
   else
     set DDS_SRC_REPOSITORY = "/hpc/internal/asi/src/dds"
   endif
endif

if ( ! -r ${DDS_SRC_REPOSITORY} ) then
   echo $0 must be run from a machine on which ${DDS_SRC_REPOSITORY} is mounted and readable
   exit 1
endif

if ( ${#argv} > 2 ) then
   set DDS_DOC_ROOT = "$2"
else if ( ! ${?DDS_DOC_ROOT} ) then
   set DDS_DOC_ROOT = "${DDSROOT}/doc"
endif

if ( -f ${DDSROOT}/FreeDDS/apps ) then
   set apps = ( `${DDSROOT}/FreeDDS/apps` )
else
   set apps = ( `find ${DDSROOT}/src/cmd -maxdepth 1 -type d | awk -F/ '{print $NF}'` )
endif

set cmdpath = "${DDS_SRC_REPOSITORY}"
set scripts = "${DDSROOT}/src/scripts"
# set types   = (scripts source ddsbrief ddslong ddssvnlog)
set types   = (scripts ddsbrief ddslong ddssvnlog)
set date    = `date`
#set awkarg  = 'BEGIN{SH=0}{if ($1==".SH") SH=1; if (SH==1 && ($1 ~ /^[a-zA-Z0-9\\\\]/)) {for(i=3;i<=NF;i++) printf "%s ",$i;exit}}'
set awkarg  = 'BEGIN{SH=0}{if ($1==".SH") SH=1; if (SH==1 && NF>2) {for(i=3;i<=NF;i++) printf "%s ",$i;exit}}'


#
#	build html files
#

foreach type ($types)

   set progs = ''

   switch ($type)
   case scripts:
      set msg = "all DDS script files"
      set script = "src.php"
      set ncol = 4;
      if ( -e  $scripts ) then
         cd $scripts
         set progs = "`ls -1 | grep -v akefile`"
      endif
      breaksw
   case source:
      set msg = "source files for all DDS modules under revision control"
      set script = "src.php"
      set ncol = 4;
      if ( -e  $cmdpath ) then
         cd $cmdpath
         set progs = `ls`
      endif
      breaksw
   case ddsbrief:
   case ddslong: 
      set msg = "manpages for all installed DDS programs under revision control"
      set script = "man.php"
      if ($type == "ddslong") then
         set ncol = 1;
      else
         set ncol = 4;
      endif
      if ( -e  $cmdpath ) then
         cd $cmdpath
         set progs = `ls -1 */man1/*.1 | awk -F/ '{print $3}' | sort`
      endif
      breaksw
   case ddssvnlog:
      set msg = "Revision conrol log files for all DDS modules"
      set script = "ddssvnlog.php"
      set ncol = 4;
      if ( -e  $cmdpath ) then
         cd $cmdpath
         set progs = `ls`
      endif
      breaksw
   endsw

   set file = "${DDS_DOC_ROOT}/html/applications/${type}.html"
   echo 
   echo "Rebuilding $file"
   rm -f $file

   #
   # build header
   #
   cat << EOF > $file
<!DOCTYPE html PUBLIC '-//W3C//DTD HTML 4.01 Transitional//EN'>
<html>
  <head>
    <title>Data Dictionary System: Applications</title>
    <meta http-equiv='Content-Type' content='text/html; charset=iso-8859-1'>
    <link href='../styles.css' type='text/css' rel='stylesheet'>
    <script src='../javascript.js' language='JavaScript'
        type='text/javascript'>
    </script>
  </head>
  <body background="../images/Background.jpg">
EOF

   echo "<script type='text/javascript'>header('applications','$type')</script>" >> $file
   echo "<center>This is a list of $msg</center>" >> $file
   echo "<center>(auto updated by buildmanlist: ${date})</center>" >> $file

   if (("$type" == "ddsbrief" || "$type" == "ddslong") && ${#apps} > 0) then
      echo "<center><span style='color:red;font-weight:bold'>&dagger;</span> NOTE: These applications are available in FreeDDS</center>" >> $file
   endif

   cat << EOF >> $file   
<table align='center' border='1' cellspacing='1' 
cellpadding='1' bgcolor='#03629C'><tr><td>
<table class='topic' border='1' cellspacing='0' cellpadding='5'><tr>
EOF

   #
   # build content
   #

   set n = 0
   foreach prog ($progs)
      set ext  = $prog:e
      set prog = $prog:r
      set dagger = ""
      if ("$type" == "scripts") then
         set mk = $DDSROOT/src/scripts/$prog
         if (! -f "$mk") continue
      else if ("$type" == "source") then
         if ( -f $DDSROOT/src/cmd/$prog/GNUmakefile ) then
           set mk = $DDSROOT/src/cmd/$prog/GNUmakefile
         else if ( -f $DDSROOT/src/cmd/$prog/Makefile ) then
           set mk = $DDSROOT/src/cmd/$prog/Makefile
         else if ( -f $DDSROOT/src/cmd/$prog/makefile ) then
           set mk = $DDSROOT/src/cmd/$prog/makefile
         endif
         if (! -f "$mk" || "$ext" == "1") continue
      else if ("$type" == "ddsbrief" || "$type" == "ddslong") then
         if (! -f $DDS_DIST/man/mbs/man1/${prog}.1) continue
         foreach app ($apps)
            if ("$app" == "$prog") then
               set dagger = "<span style='color:red;font-weight:bold;'>&dagger;</span>"
               break;
            endif
         end
      endif

      @ n = $n + 1
      if ($n > $ncol) then
         @ n = $n - $ncol
         echo "</tr><tr>" >> $file
      endif
      echo "<td class='list'>" >> $file
      echo "<a href='$script?query=$prog'>$prog</a> $dagger</td>" >> $file
      if ($type == "ddslong") then
         set desc=`awk "$awkarg" $DDS_DIST/man/mbs/man1/${prog}.1`
         echo "<td class='list'>$desc&nbsp;</td>" >> $file
      endif
   end
   while ($n < $ncol)
      @ n = $n + 1
      echo "<td class='list'>&nbsp;</td>" >> $file
   end
   echo "</tr></table></td></tr></table>" >> $file

   #
   # build footer
   #
   if (("$type" == "ddsbrief" || "$type" == "ddslong") && ${#apps} > 0) then
      echo "<center><span style='color:red;font-weight:bold'>&dagger;</span> NOTE: These applications are available in FreeDDS</center>" >> $file
   endif

   cat << EOF >> $file
    <script type='text/javascript'>footer()</script>
  </body>
</html>
EOF
 
end
