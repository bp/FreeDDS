#! /bin/csh -f
#=======================================================
# moves the binary file from current location to new 
#        directory and updates the dictionary
# --
# 12/11/97 rma
# 04/02/99 jwe
#===========================

if ( $#argv < 2 ) then
   echo "dds_mv - moves the binary file from current location "
   echo "         to new directory and updates the dictionary"
   echo " "
   echo "Usage: dds_mv <dds_dictonary_file(s)...> <new_path>"
   echo " "
   exit 1
endif

set new_path = $argv[$#argv]
set num_dicts = ` expr $#argv - 1 `

echo " "
echo "Moving $num_dicts binary file(s) to $new_path"
echo "---------------------------------------------"
set i = 1
while ($i <= $num_dicts )
   set dict = $argv[$i]
   set data = `dds_defn data < $dict`
   set data_fn = $data:t
   set data_path = $data:gh
   set data_new = ${new_path}/$data_fn

   echo "Moving $data_fn from $data_path to $new_path & updating ${dict:t}"
   if ( ${data}. == "." || ${data}. == ${dict}. || ${data}. == 'stdin:.' ) then
      echo "   *** $dict is not a DDS Dictionary ***"
   else if ( $data_path == $new_path ) then
      echo "   *** $data_fn already in $new_path ***"
   else
      if ( ! -o $dict ) then
         echo " "
         echo "   ======= ERROR ======="
         echo "   NO FILES MOVED: you do not own"
         echo "   Dictionary file: $dict"
         echo "   ---------------------"
         echo " "
      else if ( ! -o $data ) then
         echo " "
         echo "   ======= ERROR ======="
         echo "   NO FILES MOVED: you do not own"
         echo "   Binary file    : $data"
         echo "   ---------------------"
         echo " "
      else
         mv -f $data $new_path
         if ($status != 0) then
            echo "   *** unsuccessfully moved ***"
         else
            echo "data=${data_new}" >> $dict
         endif
      endif
   endif

   set i = `expr $i + 1`
end

exit 0

